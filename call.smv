MODULE main()
{
	ctos : array 0 .. 1 of ctos_channel;
	stoc : array 0 .. 1 of stoc_channel;
	client : client(stoc, ctos);
	server : server(ctos, stoc);

SPEC
	AF (ctos[0].procedure = 3);
}

MODULE client(stoc, ctos)
{
	threads : array 0 .. 1;
	for(i = 0; i < 2; i = i + 1)
		threads[i] : process client_thread(stoc[i], ctos[i]);
}

MODULE client_thread(stoc, ctos)
{
	state : {before, calling, waiting, done};
	init(state) := before;
	next(state) := switch(state) {
		before : calling;
		calling : waiting;
		waiting : stoc.ready ? done : waiting;
		done : before;
	};
	init(ctos.procedure) := 0;
	next(ctos.procedure) := case {
		state = calling : ctos.procedure + 1;
		1 : ctos.procedure;
	};
	init(ctos.ready) := 0;
	next(ctos.ready) := case {
		state = calling : 1;
		state = waiting & stoc.received : 0;
		1 : ctos.ready;
	};
	init(ctos.received) := 0;
	next(ctos.received) := case {
		state = waiting & stoc.ready : 1;
		1 : ctos.received;
	};

FAIRNESS
	running;
}

MODULE server(ctos, stoc)
{
	threads : array 0 .. 1;
	for(i = 0; i < 2; i = i + 1)
		threads[i] : process server_thread(ctos[i], stoc[i]);
}

MODULE server_thread(ctos, stoc)
{
	state : {free, returning};

	init(state) := free;
	next(state) := switch(state) {
		free : ctos.ready ? returning : free;	-- one cycle job
		returning : ctos.received ? free : returning;
	};
	init(stoc.ret) := 7;
	next(stoc.ret) := case {
		state = free & ctos.ready : ctos.procedure + 7;
		1 : stoc.ret;
	};
	init(stoc.ready) := 0;
	next(stoc.ready) := case {
		state = free & ctos.ready : 1;
		state = returning & ctos.received : 0;
		1 : stoc.ready;
	};
	init(stoc.received) := 0;
	next(stoc.received) := case {
		state = free & ctos.ready : 1;
		state = returning & ctos.received : 0;
		1 : stoc.received;
	};

FAIRNESS
	running;
}

typedef ctos_channel struct {
	received : boolean;
	procedure : 0 .. 3;
	ready : boolean;
}

typedef stoc_channel struct {
	received : boolean;
	ready : boolean;
	ret : 7 .. 10;
}